apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    defaultConfig {
        applicationId "kr.bobplanet.android"
        minSdkVersion 14
        targetSdkVersion 27
        multiDexEnabled true
    }
    dexOptions {
        javaMaxHeapSize "2g"
    }
    signingConfigs {
        debug {
            storeFile file('team-bobplanet-dev.jks')
            storePassword 'storepass'
            keyAlias 'dev'
            keyPassword 'devpass'
        }
        release
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            buildConfigField 'boolean', 'DEV_VERSION', 'true'
            buildConfigField 'String', 'BACKEND_ROOT_URL', '"https://2-dot-kr-bobplanet.appspot.com/_ah/api/"'
            signingConfig signingConfigs.debug
        }
        release {
            buildConfigField 'boolean', 'DEV_VERSION', 'false'
            buildConfigField 'String', 'BACKEND_ROOT_URL', '"https://2-dot-kr-bobplanet.appspot.com/_ah/api/"'

            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles 'proguard-rules.pro'
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
        }
    }
    productFlavors {
    }
}

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.jakewharton.hugo:hugo-plugin:1.2.1'
        classpath 'com.google.cloud.tools:endpoints-framework-gradle-plugin:1.0.3'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.hugo'
apply plugin: 'com.google.cloud.tools.endpoints-framework-client'

repositories {
    google()
    maven { url "https://s3.amazonaws.com/repo.commonsware.com" }
    maven { url "https://jitpack.io" }
    mavenCentral()
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Android Support Libraries
    implementation 'com.android.support:support-v4:27.1.1'
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:multidex:1.0.3'

    // Frameworks
    implementation 'org.greenrobot:eventbus:3.1.1'
    implementation 'com.android.volley:volley:1.1.0'
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    // Google Play Services
    implementation 'com.google.android.gms:play-services-gcm:15.0.1'

    // Google Cloud Endpoints
    endpointsServer project(path: ':backend', configuration: 'endpoints')
    implementation 'com.google.api-client:google-api-client:1.23.0'
    implementation 'com.google.http-client:google-http-client-android:1.23.0'
    implementation 'com.google.http-client:google-http-client-gson:1.23.0'

    // Analytics
    //implementation 'com.google.android.gms:play-services-analytics:16.0.0'
    implementation 'com.google.android.gms:play-services-tagmanager:16.0.0'
    implementation 'com.google.firebase:firebase-core:16.0.0'
    implementation 'com.google.firebase:firebase-analytics:16.0.0'
    //implementation 'org.piwik.sdk:piwik-sdk:3.0.2'

    // Social Logins
    implementation 'com.google.android.gms:play-services-identity:15.0.1'
    implementation 'com.google.android.gms:play-services-plus:15.0.1'
    implementation 'com.facebook.android:facebook-android-sdk:4.5.0'
    implementation 'com.commonsware.cwac:pager:0.2.6'

    // Custom UIs
    implementation 'com.github.dexafree:materiallist:3.2.1'
    implementation 'net.i2p.android.ext:floatingactionbutton:1.10.1'
    implementation 'com.github.castorflex.smoothprogressbar:library:1.1.0'
    api 'me.gujun.android.taggroup:library:1.4@aar'
    implementation 'com.github.jkwiecien:CountingTextView:1.0.10'

    // Bluetooth beacon
    implementation 'org.altbeacon:android-beacon-library:2.7.1'

}

configurations.all {
    resolutionStrategy {
        force 'com.android.support:support-v4:27.1.1'
    }
}

if (rootProject.file('secrets.properties').exists()) {
    def props = new Properties()
    props.load(new FileInputStream(rootProject.file('secrets.properties')))
    android.signingConfigs.release.storeFile file(props.get('android.signingConfigs.release.storeFile'))
    android.signingConfigs.release.storePassword props.get('android.signingConfigs.release.storePassword')
    android.signingConfigs.release.keyAlias props.get('android.signingConfigs.release.keyAlias')
    android.signingConfigs.release.keyPassword props.get('android.signingConfigs.release.keyPassword')
}

apply plugin: 'com.google.gms.google-services'