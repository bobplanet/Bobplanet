apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        applicationId "kr.bobplanet.android"
        minSdkVersion 11
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
    }

    signingConfigs {
        debug {
            storeFile file('team-bobplanet-dev.jks')
            storePassword 'storepass'
            keyAlias 'dev'
            keyPassword 'devpass'
        }
        release
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            buildConfigField 'boolean', 'DEV_VERSION', 'true'
            signingConfig signingConfigs.debug
        }
        release {
            buildConfigField 'boolean', 'DEV_VERSION', 'false'
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles 'proguard-rules.pro'
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
        }
    }
}

def props = new Properties()
props.load(new FileInputStream(rootProject.file('secrets.properties')))
android.signingConfigs.release.storeFile file(props.get('android.signingConfigs.release.storeFile'))
android.signingConfigs.release.storePassword props.get('android.signingConfigs.release.storePassword')
android.signingConfigs.release.keyAlias props.get('android.signingConfigs.release.keyAlias')
android.signingConfigs.release.keyPassword props.get('android.signingConfigs.release.keyPassword')

repositories {
    maven {
        url "https://repo.commonsware.com.s3.amazonaws.com"
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(path: ':backend', configuration: 'android-endpoints')

    compile 'com.android.support:support-v4:23.0.1'
    compile 'com.android.support:appcompat-v7:23.0.1'
    compile 'com.android.support:recyclerview-v7:23.0.1'
    compile 'com.android.support:cardview-v7:23.0.1'
    compile 'com.android.support:design:23.0.1'

    compile 'com.google.android.gms:play-services-identity:8.1.0'
    compile 'com.google.android.gms:play-services-plus:8.1.0'
    compile 'com.google.android.gms:play-services-gcm:8.1.0'
    compile 'com.google.android.gms:play-services-analytics:8.1.0'

    compile 'com.commonsware.cwac:pager:0.2.+'
    compile 'de.greenrobot:eventbus:2.4.0'
    compile 'com.mcxiaoke.volley:library:1.0.19'
    compile 'com.github.castorflex.smoothprogressbar:library:1.0.0'
}