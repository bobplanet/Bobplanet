// If you would like more information on the gradle-appengine-plugin please refer to the github page
// https://github.com/GoogleCloudPlatform/gradle-appengine-plugin

buildscript {
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.google.appengine:gradle-appengine-plugin:1.9.26'
        classpath "gradle.plugin.de.fuerstenau:BuildConfigPlugin:1.0.5"
    }
}

repositories {
    jcenter();
}

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'appengine'
apply plugin: 'de.fuerstenau.buildconfig'

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

dependencies {
    appengineSdk 'com.google.appengine:appengine-java-sdk:1.9.28'
    compile 'com.google.appengine:appengine-endpoints:1.9.28'
    compile 'com.google.appengine:appengine-endpoints-deps:1.9.28'
    compile 'com.googlecode.objectify:objectify:5.1.8'
    compile 'com.ganyo:gcm-server:1.0.2'
    compile 'javax.servlet:servlet-api:2.5'
    compile 'joda-time:joda-time:2.8.2'
}

appengine {
    downloadSdk = true
    appcfg {
        oauth2 = true
        email = "heonkyu.jin@gmail.com"
    }
    endpoints {
        getClientLibsOnBuild = true
        getDiscoveryDocsOnBuild = true
    }
}

def props = new Properties()
if (rootProject.file('secrets.properties').exists()) {
    props.load(new FileInputStream(rootProject.file('secrets.properties')))
}

buildConfig {
    sourceSets {
        main {
            packageName = "kr.bobplanet.backend"
            buildConfigField "String", "CLIENTID_ANDROID_DEV", props.get("backend.clientid.android.dev")
            buildConfigField "String", "CLIENTID_ANDROID_RELEASE", props.get("backend.clientid.android.release")
            buildConfigField "String", "CLIENTID_WEB", props.get("backend.clientid.web")
            buildConfigField "String", "GCM_API_KEY", props.get("backend.gcm.apikey")
        }
    }
}